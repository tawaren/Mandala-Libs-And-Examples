import std.Token._
import std.Token.mint as tokenMint
import std.Option._
import std.SubjectManager._
import std.EdDsaIssuer._
import std.EdDsaIssuer
import std.core.sys.EdDsa
import std.core.sys.Sys._
import std.core.IntU128.U128
import std.SubjectLock._
import std.core.Equal._

module UzhToken {
    //Marker type for our token
    local data UZH

    //Note: 0x0000000000000000000000000000000000000000000000000000000000000000 is a placeholder
    //Replace 0x00..00 with a Pk where you have access to the corresponding SK
    global function ownerPk() = 0xdf797acae07febf43fef6585f87c0529a9580330a8c97b621a0fcc7a4968558b:EdDsa.Pk
    global function ownerSubject() = subjectFor(ownerPk()).subject

    global transactional function mintUZH(amount:U128, auth:AuthenticationToken):Token[UZH] {
        match ownerSubject() == auth.subject with
         | False => rollback
         | True => tokenMint[UZH](amount)
    }

    global function safeMintUZH(amount:U128, auth:AuthenticationToken):Option[Token[UZH]] {
        try mintUZH(amount,auth) with
         | success(tok) => Option#Some(tok)
         | failure => Option#None
    }

}

transactional transaction MintUZH(amount:U128, auth:AuthenticationToken, context consume gen:IdGenerator):(coins:Entry[Locked[Token[UzhToken.UZH]]]) {
    let newTokens = UzhToken.mintUZH(amount, auth) in
    let (storageLoc, _) = uniqueID(gen) in
    lockEntry(auth.subject,storageLoc,newTokens)
}


transactional transaction TransferUZH(consume coins:Entry[Locked[Token[UzhToken.UZH]]], auth:AuthenticationToken, amount:U128, to:Subject, context consume gen:IdGenerator):
                                  (coinsChange:Entry[Locked[Token[UzhToken.UZH]]], coinsSent:Entry[Locked[Token[UzhToken.UZH]]]) {
  let (senderLoc, allTokens) = unlockEntry(coins, auth) in
  let (rem,send) = split(allTokens, amount) in
  let (receiverLoc, _) = uniqueID(gen) in
  let changeEntry = lockEntry(auth.subject,senderLoc,rem) in
  let receiverEntry = lockEntry(to,receiverLoc,send) in
  (changeEntry, receiverEntry)
}